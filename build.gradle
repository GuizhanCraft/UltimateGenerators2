plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.freefair.lombok' version '8.6'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group = 'net.guizhanss'
version = '0.1.0-alpha'
description = 'UltemateGenerators2'

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://jitpack.io')
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'com.github.Slimefun:Slimefun4:da9c2ac4cc'
    implementation 'net.guizhanss:GuizhanLib-api:1.7.6'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly 'net.guizhanss:SlimefunTranslation:59724ec5a1'
}

java.sourceCompatibility = JavaVersion.VERSION_16

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


shadowJar {
    relocate 'net.guizhanss.guizhanlib', 'net.guizhanss.ultimategenerators2.libs.guizhanlib'
    relocate 'org.bstats', 'net.guizhanss.ultimategenerators2.libs.bstats'
    minimize()
    archiveClassifier = ''
}

bukkit {
    main = 'net.guizhanss.ultimategenerators2.UltimateGenerators2'
    apiVersion = '1.16'
    authors = ['ybw0014']
    description = 'A Slimefun Addon that adds various types of generators.'
    depend = ['Slimefun']
    softDepend = ['GuizhanLibPlugin', 'SlimefunTranslation']
}
